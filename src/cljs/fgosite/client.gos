// -*- mode: go -*-
package client

import macros hiccups   "hiccups/core"

const jQuery = \`js/jQuery`


func<hiccups.defhtml> myTemplate() {
	[DIV.CONTAINER,
		[DIV.ROW,
			[TEXTAREA#FGO.FORM_CONTROL.COL_MD_6, {ROWS: 10},
				`package main

import "fmt"

func main() {
    fmt.Println("Hello, 世界")
}`
			],
			[PRE#CLJ.COL_MD_6,
				"Clojure code goes here"
			]
		],
		[DIV.ROW,
			[TEXTAREA.FORM_CONTROL.COL_MD_12, {ROWS: 10},
				"Output goes here"
			]
		]
	]
}

func ajaxPut(url, data, success) {
	jQuery->ajax(jsObj(
		"type",    "PUT",
		"url",     url,
		"data",    data,
		"success", success
	))
}

func ajaxGet(url, success, error) {
	jQuery->ajax(jsObj(
		"url",     url,
		"success", success,
		"error",    error
	))
}

func core(fgoElem, cljElem) {
	func getClj() {
		const(
			fgoText = fgoElem->val()
			prefix  = "/"  str  js.md5(fgoText)
		)
		ajaxGet(
			prefix  str "/clj",
			func{cljElem->html($1)},
			func(){
				ajaxPut(prefix  str  "/fgo", fgoText, getClj)
			}
		)
	}

	fgoElem->keyup(getClj)
	getClj()
}

jQuery(func(){
	const(
		insertHere = jQuery("#insert-here")
	)
	insertHere->html(myTemplate())
	core(jQuery("#fgo"), jQuery("#clj"))
})

