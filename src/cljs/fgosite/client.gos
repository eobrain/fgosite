// -*- mode: go -*-
package client

import macros hiccups   "hiccups/core"

const jQuery = \`js/jQuery`


func<hiccups.defhtml> myTemplate() {
	[DIV.CONTAINER,
		[DIV.ROW,
			[TEXTAREA#FGO.FORM_CONTROL.COL_MD_6, {ROWS: 10},
				`package main

import "fmt"

func main() {
    fmt.Println("Hello, 世界")
}`
			],
			[TEXTAREA#CLJ.FORM_CONTROL.COL_MD_6, {ROWS: 10},
				"Clojure code goes here"
			]
		],
		[DIV.ROW,
			[TEXTAREA.FORM_CONTROL.COL_MD_12, {ROWS: 10},
				"Output goes here"
			]
		]
	]
}

func ajaxPut(url, data, success) {
	jQuery->ajax(jsObj(
		"type",    "PUT",
		"url",     url,
		"data",    data,
		"success", success
	))
}

func ajaxGet(url, resultIs) {
	jQuery->get(url, resultIs)
}

jQuery(func(){
	const insertHere = js.document->getElementById("insert-here")
	insertHere->_innerHTML  mutateSet  myTemplate()
	{
		const(
			fgoText = jQuery("#fgo")->text()
			prefix  = "/"  str  js.md5(fgoText)
		)
		func getClj() {
			ajaxGet(prefix  str "/clj",  func(cljText){
				jQuery("#clj")->html(cljText)
			})
		}

		jQuery(js.document)->ajaxError(func(){
			ajaxPut(prefix  str  "/fgo", fgoText, getClj)
		})

		getClj()
	}
})

